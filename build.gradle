plugins {
    id 'dev.architectury.loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

loom {
    forge {
        mixinConfigs = [
                "reeses-sodium-options.mixins.json"
        ]
    }
}

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"

    modImplementation "maven.modrinth:embeddium:${project.sodium_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from "LICENSE"
}

def changelogText = file("changelog.md").getText('UTF-8')

modrinth {
    token = System.getenv("MODRINTH_TOKEN")

    projectId = "textrues-rubidium-options"
    versionNumber = "${project.version}"
    versionType = "beta"
    changelog = changelogText
    uploadFile = remapJar
    additionalFiles = [sourcesJar]
    gameVersions = ["1.18", "1.18.2"]
    loaders = ["forge"]
    dependencies {
        required.project "embeddium"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    disableVersionDetection()

    def mainFile = upload(910506, remapJar)
    mainFile.displayName = "${project.version}"
    mainFile.releaseType = "beta"
    mainFile.changelog = file("changelog.md")
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addJavaVersion("Java 17", "Java 18")
    mainFile.addGameVersion("1.18", "1.18.2")
    mainFile.addRequirement("embeddium")

    def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    sourcesFile.changelog = file("changelog.md")
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
    }
}
