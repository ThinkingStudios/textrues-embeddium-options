plugins {
    id 'maven-publish'
    alias(libs.plugins.architectury.loom)
    alias(libs.plugins.modpublisher)
}

base.archivesName = libs.versions.archives.name.get()
version = "${libs.versions.mod.get()}+mc${libs.versions.minecraft.get()}"
group = libs.versions.maven.group.get()

repositories {
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://maven.neoforged.net/releases/" }
}

dependencies {
    minecraft(libs.minecraft)
    mappings(libs.yarn) { artifact { classifier = "v2" } }
    neoForge(libs.neoforge)

    modImplementation(libs.embeddium)
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publisher {
    apiKeys {
        modrinth System.getenv("MODRINTH_TOKEN")
        curseforge System.getenv("CURSE_TOKEN")
    }

    setCurseID("${libs.versions.id.curseforge.get()}")
    setModrinthID("${libs.versions.id.modrinth.get()}")
    setVersionType("beta")
    setChangelog(file("changelog.md").getText("UTF-8"))
    setVersion("${project.version}")
    setDisplayName("${project.version}")
    setGameVersions("1.20.3", "1.20.4")
    setLoaders("${libs.versions.loader.get()}")
    setCurseEnvironment("client")
    setArtifact(remapJar)
    addAdditionalFile(sourcesJar)

    modrinthDepends {
        required "embeddium"
    }

    curseDepends {
        required "embeddium"
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {

    }
}